{% extends "base_livecode.html" %}

{% block title %}Running Backwards, Part 1: '(I love you){% endblock %}

{% block content %}

This interactive tutorial is based on the video <a href="http://youtu.be/kS_2_-E_JjU">Running Backwards, Part 1: (I love you)</a>, the first in a series of videos on relational programming.  Click the triangle below a code snippet to run the code.  Click on a box containing the code in order to edit the code, which you can then run by clicking on the triangle below it.

<h2>(I love you)</h2>
<p>
Matt Might's blog post <a href="http://matt.might.net/articles/i-love-you-in-racket/">99 ways to say <code>'(I love you)</code> in Racket</a>, contains...99 programs that evaluate to the list <code>(I love you)</code> in the <a href="http://racket-lang.org/">Racket</a> programming language.  We will be using the Scheme programming language instead of Racket---most or all of these programs will evaluate identically in both languages.
</p>

<p>Here are the four <code>(I love you)</code> programs I evaluated in the video.</p>

<div class="live" id="love1">
(quote (I love you))</div>

<div class="live" id="love2">
(list (quote I) (quote love) (quote you))</div>

<div class="live" id="love3">
(cons 'I (cons 'love (cons 'you '())))</div>

<div class="live" id="love4">
(let ([words '(love you I)])
  (list (car (cdr (cdr words)))
        (car words)
        (car (cdr words))))</div>

Feel free to edit one of the examples above to include your own code, or paste in an example from Matt's blog post.

<div class="live" id="mk1">
(run 1 (expr) (evalo expr '(I love you)))</div>

<div class="live" id="mk2">
(run 2 (expr) (evalo expr '(I love you)))</div>

<div class="live" id="genlove1">
(list 'I 'love 'you)</div>

<div class="live" id="mk3">
(run 99 (expr) (evalo expr '(I love you))) </div>

<div class="live" id="genlove2">
(car (cons '(I love you) (list)))</div>

<div class="live" id="genlove3a">
((list (car '(I . _.0)) (car '(love . _.1))
       ((lambda () 'you)))
 (absent closure _.0) (absent closure _.1))</div>

<div class="live" id="genlove3b">
(list (car '(I . _.0)) (car '(love . _.1))
      ((lambda () 'you)))</div>

<div class="live" id="genlove3c">
(list (car '(I . 5)) (car '(love . _.1))
     ((lambda () 'you)))</div>

<div class="live" id="genlove3d">
(list (car '(I . (a b c))) (car '(love . _.1))
     ((lambda () 'you)))</div>

<p>
Let's time how long it takes to generate 99 answers.  (On my laptop it takes around 8 milliseconds.)
</p>

<div class="live" id="time99">
(time (length (run 99 (expr) (evalo expr '(I love you)))))</div>

<p>
To be kind to the server running this code, let's try a run 99 instead of a run 999 for our
<code>(I love lamp)</code> example.
</p>

<div class="live" id="lamp1">
(run 99 (expr) (evalo expr '(I love lamp)))</div>

If we were to replace <code>run 99</code> with <code>run 999</code> in the expression above, the last answer would be:

<div class="live" id="lovelamp1">
(list (car '(I . _.0)) 'love
        ((lambda (_.1) (cdr _.1)) '(_.2 . lamp)))</div>


<p>
Just for fun, here are all 99 of Matt's example expressions that evaluate to <code>(I love you)</code> in Racket.  All, or almost all, of these examples should work in Scheme as well.
</p> 


<div class="live" id="loveex0">
'(I love you)</div>

<div class="live" id="loveex1">
(quote (I love you))</div>

<div class="live" id="loveex2">
(list 'I 'love 'you)</div>

<div class="live" id="loveex3">
(list (quote I) (quote love) (quote you))</div>

<div class="live" id="loveex4">
(cons 'I (cons 'love (cons 'you '())))</div>

<div class="live" id="loveex5">
(cons 'I (cons 'love (list 'you)))</div>

<div class="live" id="loveex6">
(cons 'I (list 'love 'you))</div>

<div class="live" id="loveex7">
(cons 'I '(love you))</div>

<div class="live" id="loveex8">
'(I love . (you))</div>

<div class="live" id="loveex9">
'(I . (love . (you)))</div>

<div class="live" id="loveex10">
'(I . (love . (you . ())))</div>

<div class="live" id="loveex11">
'(I . (love you))</div>

<div class="live" id="loveex12">
`(I love you)</div>

<div class="live" id="loveex13">
`(I ,'love you)</div>

<div class="live" id="loveexdoh1">
(quasiquote (I ,'love  you))</div>

<div class="live" id="loveexdoh2">
(quasiquote (I (unquote 'love) you))</div>

<div class="live" id="loveex14">
`(I ,`love you)</div>

<div class="live" id="loveex15">
(let ([verb 'love])
  `(I ,verb you))</div>

<div class="live" id="loveex16">
`(I ,(string->symbol "love") you)</div>

<div class="live" id="loveex17">
`(I ,(string->symbol (list->string '(#\l #\o #\v #\e))) you)</div>

<div class="live" id="loveex18">
`(I love . (you))</div>

<div class="live" id="loveex19">
`(I love . ,(list 'you))</div>

<div class="live" id="loveex20">
`(I love ,@'(you))</div>

<div class="live" id="loveex21">
`(I love (unquote-splicing '(you)))</div>

<div class="live" id="loveex22">
`(I ,@(list 'love 'you))</div>

<div class="live" id="loveex23">
`(,@(list 'I 'love) you)</div>

<div class="live" id="loveex24">
`(,@'(I love you))</div>

<div class="live" id="loveex25">
`,'(I love you)</div>

<div class="live" id="loveex26">
`(I love you . ,'())</div>

<div class="live" id="loveex27">
(car (list '(I love you)))</div>

<div class="live" id="loveex28">
(cdr '(Hark! I love you))</div>

<div class="live" id="loveex29">
(let ([words '(love you I)])
  (list (car (cdr (cdr words)))
        (car words)
        (car (cdr words))))</div>

<div class="live" id="loveex30">
(let ([words '(love you I)])
  (list (caddr words)
        (car words)
        (cadr words)))</div>

<div class="live" id="loveex31">
(let* ([c '(you)]
       [b (cons 'love c)]
       [a (cons 'I b)])
  a)</div>

<div class="live" id="loveex32">
(let ()
  '(I love you not)
  '(I love you))</div>

<div class="live" id="loveex33">
(vector->list (vector 'I 'love 'you))</div>

<div class="live" id="loveex34">
(vector->list #(I love you))</div>

<div class="live" id="loveex35">
(stream->list (stream 'I 'love 'you))</div>

<div class="live" id="loveex36">
((lambda args args) 'I 'love 'you)</div>

<div class="live" id="loveex37">
((lambda (one two . rest) rest) 'You 'believe 'I 'love 'you)</div>

<div class="live" id="loveex38">
((lambda (a c b) (list a b c)) 'I 'you 'love)</div>

<div class="live" id="loveex39">
(apply (lambda (a c b) (list a b c)) 
       (list 'I 'you 'love))</div>

<div class="live" id="loveex40">
((lambda (a b [c 'you]) (list a b c)) 'I 'love)</div>

<div class="live" id="loveex41">
((lambda (#:foo b #:bar c #:baz a)
   (list a b c))
 #:baz 'I #:bar 'you #:foo 'love)</div>

<div class="live" id="loveex  42">
((lambda (a b #:key [c 'me]) (list a b c)) #:key 'you 'I 'love)</div>

<div class="live" id="loveex43">
(let ([f (lambda (x)
           (lambda (y)
             (lambda (z)
               (list x y z))))])
  (((f 'I) 'love) 'you))</div>

<div class="live" id="loveex44">
(let ([f (case-lambda 
           [() 'I]
           [(x) 'love]
           [(x y) 'you])])
  (list (f) (f 1) (f 1 2)))</div>

<div class="live" id="loveex45">
(append '(I love) '(you))</div>

<div class="live" id="loveex46">
(append '(I) '(love) '(you))</div>

<div class="live" id="loveex47">
(flatten '((I) (love you)))</div>

<div class="live" id="loveex48">
(flatten '((I) (love) (you) ()))</div>

<div class="live" id="loveex49">
(reverse '(you love I))</div>

<div class="live" id="loveex50">
(remove 'cannot '(I cannot love you))</div>

<div class="live" id="loveex51">
(remove-duplicates '(I love love love you))</div>

<div class="live" id="loveex52">
(take '(I love you not) 3)</div>

<div class="live" id="loveexdoh3">
(take-right '(I think I love you) 3)</div>

<div class="live" id="loveex53">
(drop '(She knows I love you) 2)</div>

<div class="live" id="loveex54">
(drop-right '(I love you no more) 2)</div>

<div class="live" id="loveex55">
(map (lambda (x) (if (eq? x 'hate) 'love x))
     '(I hate you))</div>

<div class="live" id="loveex56">
(map (lambda (i) (vector-ref #(love you I) i))
     '(2 0 1))</div>

<div class="live" id="loveex57">
(map (lambda (k) (hash-ref #hash(("foo" . I) 
                            ("baz" . you)
                            ("bar" . love)) k))
     '("foo" "bar" "baz"))</div>
     
<div class="live" id="loveex58">
(map string->symbol (sort (list "love" "you" "I") string<?))</div>

<div class="live" id="loveex59">
(map string->symbol (string-split "I-love-you" "-"))</div>

<div class="live" id="loveex60">
(flatten (map (lambda (a b) (cons a b))
              '(I love you)
              '(() () ())))</div>

<div class="live" id="loveex  61">
(filter (lambda (x) (not (eq? x 'cannot))) 
        '(I cannot love you))</div>

<div class="live" id="loveex62">
(foldr cons '() '(I love you))</div>

<div class="live" id="loveex63">
(foldl cons '() '(you love I))</div>

<div class="live" id="loveex64">
(for/list ([word #(I love you)])
  word)</div>

<div class="live" id="loveex65">
(cond
  [(even? 3) '(Not me)]
  [(odd?  3) '(I love you)])</div>
  
<div class="live" id="loveex66">
(cond
  [(even? 3) '(Not me)]
  [(odd?  2) '(Nor I)]
  [else      '(I love you)])</div>
  
<div class="live" id="loveex67">
(case 1
  [(a b c) '(Not me)]
  [(3 2 1) '(I love you)])</div>

<div class="live" id="loveex68">
(match #t
  [#f '(Not me)]
  [#t '(I love you)])</div>

<div class="live" id="loveex69">
(match #t
  [#f '(Not me)]
  [_  '(I love you)])</div>

<div class="live" id="loveex70">
(match 'you
  ['me '(Not me)]
  [x   `(I love ,x)])</div>

<div class="live" id="loveex71">
(match '(foo bar)
  ['(foo bar) '(I love you)])</div>

<div class="live" id="loveex72">
(match '(I cannot lift you)
  [(list 'I 'cannot _ c) `(I love ,c)])</div>

<div class="live" id="loveex73">
(match '(2 3 1)
  [(list-no-order 3 1 2)
   '(I love you)])</div>

<div class="live" id="loveex74">
(match '(love you I)
  [(list-no-order 'I 'love foo)
   `(I love ,foo)])</div>

<div class="live" id="loveex75">
(match '(3 . 4)
  [(cons 3 4)
   '(I love you)])</div>

<div class="live" id="loveex76">
(match '(3 love 1)
  [(cons 3 (cons x (cons 1 '())))
   `(I ,x you)])</div>

<div class="live" id="loveex77">
(match '(3 love 1)
  [(cons 3 (cons x (cons 1 '())))
   `(I (unquote x) you)])</div>

<div class="live" id="loveex78">
(match 3
  [(? symbol?) '(Not me)]
  [(? string?) '(Me neither)]
  [(? number?) '(I love you)])</div>

<div class="live" id="loveex79">
(match 3
  [(not 4)   '(I love you)]
  [3         'unreachable])</div>

<div class="live" id="loveex  80">
(match '(you love I)
  [`(,c love ,a)
   `(,a love ,c)])</div>

<div class="live" id="loveex81">
(match '(We love you)
  [`(,_ . ,rest)
   `(I . ,rest)])</div>

<div class="live" id="loveex82">
(match '(We love you)
  [`(,_ ,rest ...)
   `(I ,@rest)])</div>

<div class="live" id="loveex83">
(match '(We love you)
  [(list _ rest ...)
   `(I ,@rest)])</div>

<div class="live" id="loveex84">
(match #(1 love 3)
  [(vector (? number?) b (? number?))
   `(I ,b you)])</div>

<div class="live" id="loveex85">
(match #hash((1 . I) (3 . you) (5 . love))
  [(hash-table (1 a) (5 b) (3 c))
   (list a b c)])</div>

<div class="live" id="loveex86">
(match 'you
  [(and x (? symbol?)) `(I love ,x)])</div>

<div class="live" id="loveex87">
(match '100
  [(app (lambda (n) (- n 1)) 99)
   '(I love you)])</div>

<div class="live" id="loveex88">
(list 'I 
      (call/cc (lambda (cc)
                 (error (cc 'love))))
      'you)</div>

<div class="live" id="loveex89">
(with-handlers ([symbol? (lambda (p)
                             `(I ,p you))])
    (raise 'love))</div>

<div class="live" id="loveex90">
(let ([problem (delay (car '()))])
  '(I love you))</div>

<div class="live" id="loveex91">
`(I ,(force (delay 'love)) you)</div>
 
<div class="live" id="loveex92">
(letrec ([x (delay (list a b c))]
         [a 'I]
         [c 'you]
         [b 'love])
  (force x))</div>

<div class="live" id="loveex93">
(let ([word 'know])
  (set! word 'love)
  `(I ,word you))</div>

<div class="live" id="loveex94">
(let ([word-box (box 'know)])
  (set-box! word-box 'love)
  `(I ,(unbox word-box) you))</div>

<div class="live" id="loveex95">
(let-syntax ([un-yoda-list
              (syntax-rules ()
                [(_ c a b) (list 'a 'b 'c)])])
  (un-yoda-list you I love))</div>

<div class="live" id="loveex96">
(let ((in (open-input-string "I love you")))
  (cons (read in)
        (cons (read in)
              (cons (read in) '()))))</div>

<div class="live" id="loveex97">
(list (touch (future (lambda () 'I))) 'love 'you)</div>

<div class="live" id="loveex98">
(let ([a (make-parameter "a")] 
      [b (make-parameter "b")]
      [c (make-parameter "c")])
  (parameterize ([a 'i] [b 'love] [c 'you])
    (list (a) 
          ((parameterize ([b 'dislike])
             (lambda () (b))))
          (c))))</div>

{% endblock %}
